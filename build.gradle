buildscript {
	ext {
		NEXUS_REPO_URL = 'http://192.168.1.31:8081/repository/amhzing'

		springBootVersion = '1.3.5.RELEASE'
        gradleSemanticVersion = '1.1.1'


		logbackVersion = '1.1.7'
		logstashLogbackVersion = '4.7'
		janinoVersion = '2.7.8'
		slf4jVersion = '1.7.21'
	}
	repositories {
		mavenCentral()

        maven {
            url "https://plugins.gradle.org/m2/"
        }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.net.vivin:gradle-semantic-build-versioning:${gradleSemanticVersion}")

	}
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'spring-boot'
apply plugin: 'net.vivin.gradle-semantic-build-versioning'

group = 'com.amhzing.activities-config'

project.version.with {
    startingVersion = '1.0.0'
}

jar {
	baseName = 'activities-config-microservice'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-actuator-docs')
	compile('org.springframework.boot:spring-boot-starter-hateoas')
	compile('org.springframework.boot:spring-boot-starter-web')

	compile('org.springframework.cloud:spring-cloud-config-server')

	compile ("ch.qos.logback:logback-classic:${logbackVersion}")
	compile ("ch.qos.logback:logback-core:${logbackVersion}")
	compile ("net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")
	compile ("org.codehaus.janino:janino:${janinoVersion}")
	compile ("org.slf4j:slf4j-api:${slf4jVersion}")

	testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
	imports { 
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:Brixton.SR1" 
	}
}

// Make the System.properties available in the bootRun task.
bootRun.systemProperties = System.properties

uploadArchives {
	repositories {
		mavenDeployer {
			repository(url: "${NEXUS_REPO_URL}-releases") {
				authentication(userName: mavenUser, password: mavenPassword)
			}
			snapshotRepository(url: "${NEXUS_REPO_URL}-snapshots") {
				authentication(userName: mavenUser, password: mavenPassword)
			}
		}
	}
}