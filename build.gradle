buildscript {
	ext {
		NEXUS_REPO_URL = 'http://192.168.1.31:8082/nexus/content/repositories'

		springBootVersion = '1.3.5.RELEASE'
		sshVersion = '2.2.0'
		gradleReleaseVersion = '2.4.0'

		logbackVersion = '1.1.7'
		logstashLogbackVersion = '4.7'
		janinoVersion = '2.7.8'
		slf4jVersion = '1.7.21'
	}
	repositories {
		mavenCentral()

		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.hidetake:gradle-ssh-plugin:${sshVersion}")
		classpath("net.researchgate:gradle-release:${gradleReleaseVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'spring-boot'
apply plugin: 'org.hidetake.ssh'
apply plugin: 'net.researchgate.release'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-actuator-docs')
	compile('org.springframework.boot:spring-boot-starter-hateoas')
	compile('org.springframework.boot:spring-boot-starter-web')

	compile('org.springframework.cloud:spring-cloud-config-server')

	compile ("ch.qos.logback:logback-classic:${logbackVersion}")
	compile ("ch.qos.logback:logback-core:${logbackVersion}")
	compile ("net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")
	compile ("org.codehaus.janino:janino:${janinoVersion}")
	compile ("org.slf4j:slf4j-api:${slf4jVersion}")

	testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
	imports { 
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:Brixton.SR1" 
	}
}

// Create a fully executable jar
springBoot {
    executable = true
}

// Make the System.properties available in the bootRun task.
bootRun.systemProperties = System.properties

// Test
apply from: 'test.gradle'

// Publish to Nexus
apply from: 'publish.gradle'

// Deploy
apply from: 'deploy.gradle'

// Automatically expand info properties
processResources {
	filesMatching('**/*.yml') { expand(project.properties) }
}

ext.jenkinsJobName = System.env.JOB_NAME
ext.jenkinsBuildTimestamp = System.env.BUILD_TIMESTAMP
ext.jenkinsBuildId = System.env.BUILD_ID
ext.jenkinsBuildNumber = System.env.BUILD_NUMBER
ext.jenkinsGitCommit = System.env.GIT_COMMIT
ext.jenkinsGitBranch = System.env.GIT_BRANCH